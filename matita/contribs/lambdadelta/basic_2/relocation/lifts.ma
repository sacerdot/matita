
(**************************************************************************)
(*       ___                                                              *)
(*      ||M||                                                             *)
(*      ||A||       A project by Andrea Asperti                           *)
(*      ||T||                                                             *)
(*      ||I||       Developers:                                           *)
(*      ||T||         The HELM team.                                      *)
(*      ||A||         http://helm.cs.unibo.it                             *)
(*      \   /                                                             *)
(*       \ /        This file is distributed under the terms of the       *)
(*        v         GNU General Public License Version 2                  *)
(*                                                                        *)
(**************************************************************************)

include "ground_2/relocation/nstream_after.ma".
include "basic_2/notation/relations/rliftstar_3.ma".
include "basic_2/grammar/term.ma".

(* GENERIC RELOCATION FOR TERMS *********************************************)

(* Basic_1: includes:
            lift_sort lift_lref_lt lift_lref_ge lift_bind lift_flat
            lifts_nil lifts_cons
*)
inductive lifts: rtmap â†’ relation term â‰
| lifts_sort: âˆ€s,f. lifts f (â‹†s) (â‹†s)
| lifts_lref: âˆ€i1,i2,f. @â¦ƒi1, fâ¦„ â‰¡ i2 â†’ lifts f (#i1) (#i2)
| lifts_gref: âˆ€l,f. lifts f (Â§l) (Â§l)
| lifts_bind: âˆ€p,I,V1,V2,T1,T2,f.
              lifts f V1 V2 â†’ lifts (â†‘f) T1 T2 â†’
              lifts f (â“‘{p,I}V1.T1) (â“‘{p,I}V2.T2)
| lifts_flat: âˆ€I,V1,V2,T1,T2,f.
              lifts f V1 V2 â†’ lifts f T1 T2 â†’
              lifts f (â“•{I}V1.T1) (â“•{I}V2.T2)
.

interpretation "uniform relocation (term)"
   'RLiftStar i T1 T2 = (lifts (uni i) T1 T2).

interpretation "generic relocation (term)"
   'RLiftStar f T1 T2 = (lifts f T1 T2).


(* Basic inversion lemmas ***************************************************)

fact lifts_inv_sort1_aux: âˆ€X,Y,f. â¬†*[f] X â‰¡ Y â†’ âˆ€s. X = â‹†s â†’ Y = â‹†s.
#X #Y #f * -X -Y -f //
[ #i1 #i2 #f #_ #x #H destruct
| #p #I #V1 #V2 #T1 #T2 #f #_ #_ #x #H destruct
| #I #V1 #V2 #T1 #T2 #f #_ #_ #x #H destruct
]
qed-.

(* Basic_1: was: lift1_sort *)
(* Basic_2A1: includes: lift_inv_sort1 *)
lemma lifts_inv_sort1: âˆ€Y,s,f. â¬†*[f] â‹†s â‰¡ Y â†’ Y = â‹†s.
/2 width=4 by lifts_inv_sort1_aux/ qed-.

fact lifts_inv_lref1_aux: âˆ€X,Y,f. â¬†*[f] X â‰¡ Y â†’ âˆ€i1. X = #i1 â†’
                          âˆƒâˆƒi2. @â¦ƒi1, fâ¦„ â‰¡ i2 & Y = #i2.
#X #Y #f * -X -Y -f
[ #s #f #x #H destruct
| #i1 #i2 #f #Hi12 #x #H destruct /2 width=3 by ex2_intro/
| #l #f #x #H destruct
| #p #I #V1 #V2 #T1 #T2 #f #_ #_ #x #H destruct
| #I #V1 #V2 #T1 #T2 #f #_ #_ #x #H destruct
]
qed-.

(* Basic_1: was: lift1_lref *)
(* Basic_2A1: includes: lift_inv_lref1 lift_inv_lref1_lt lift_inv_lref1_ge *)
lemma lifts_inv_lref1: âˆ€Y,i1,f. â¬†*[f] #i1 â‰¡ Y â†’
                       âˆƒâˆƒi2. @â¦ƒi1, fâ¦„ â‰¡ i2 & Y = #i2.
/2 width=3 by lifts_inv_lref1_aux/ qed-.

fact lifts_inv_gref1_aux: âˆ€X,Y,f. â¬†*[f] X â‰¡ Y â†’ âˆ€l. X = Â§l â†’ Y = Â§l.
#X #Y #f * -X -Y -f //
[ #i1 #i2 #f #_ #x #H destruct
| #p #I #V1 #V2 #T1 #T2 #f #_ #_ #x #H destruct
| #I #V1 #V2 #T1 #T2 #f #_ #_ #x #H destruct
]
qed-.

(* Basic_2A1: includes: lift_inv_gref1 *)
lemma lifts_inv_gref1: âˆ€Y,l,f. â¬†*[f] Â§l â‰¡ Y â†’ Y = Â§l.
/2 width=4 by lifts_inv_gref1_aux/ qed-.

fact lifts_inv_bind1_aux: âˆ€X,Y,f. â¬†*[f] X â‰¡ Y â†’
                          âˆ€p,I,V1,T1. X = â“‘{p,I}V1.T1 â†’
                          âˆƒâˆƒV2,T2. â¬†*[f] V1 â‰¡ V2 & â¬†*[â†‘f] T1 â‰¡ T2 &
                                   Y = â“‘{p,I}V2.T2.
#X #Y #f * -X -Y -f
[ #s #f #q #J #W1 #U1 #H destruct
| #i1 #i2 #f #_ #q #J #W1 #U1 #H destruct
| #l #f #b #J #W1 #U1 #H destruct
| #p #I #V1 #V2 #T1 #T2 #f #HV12 #HT12 #q #J #W1 #U1 #H destruct /2 width=5 by ex3_2_intro/
| #I #V1 #V2 #T1 #T2 #f #_ #_ #q #J #W1 #U1 #H destruct
]
qed-.

(* Basic_1: was: lift1_bind *)
(* Basic_2A1: includes: lift_inv_bind1 *)
lemma lifts_inv_bind1: âˆ€p,I,V1,T1,Y,f. â¬†*[f] â“‘{p,I}V1.T1 â‰¡ Y â†’
                       âˆƒâˆƒV2,T2. â¬†*[f] V1 â‰¡ V2 & â¬†*[â†‘f] T1 â‰¡ T2 &
                                Y = â“‘{p,I}V2.T2.
/2 width=3 by lifts_inv_bind1_aux/ qed-.

fact lifts_inv_flat1_aux: âˆ€X,Y. âˆ€f:rtmap. â¬†*[f] X â‰¡ Y â†’
                          âˆ€I,V1,T1. X = â“•{I}V1.T1 â†’
                          âˆƒâˆƒV2,T2. â¬†*[f] V1 â‰¡ V2 & â¬†*[f] T1 â‰¡ T2 &
                                   Y = â“•{I}V2.T2.
#X #Y #f * -X -Y -f
[ #s #f #J #W1 #U1 #H destruct
| #i1 #i2 #f #_ #J #W1 #U1 #H destruct
| #l #f #J #W1 #U1 #H destruct
| #p #I #V1 #V2 #T1 #T2 #f #_ #_ #J #W1 #U1 #H destruct
| #I #V1 #V2 #T1 #T2 #f #HV12 #HT12 #J #W1 #U1 #H destruct /2 width=5 by ex3_2_intro/
]
qed-.

(* Basic_1: was: lift1_flat *)
(* Basic_2A1: includes: lift_inv_flat1 *)
lemma lifts_inv_flat1: âˆ€I,V1,T1,Y. âˆ€f:rtmap. â¬†*[f] â“•{I}V1.T1 â‰¡ Y â†’
                       âˆƒâˆƒV2,T2. â¬†*[f] V1 â‰¡ V2 & â¬†*[f] T1 â‰¡ T2 &
                                Y = â“•{I}V2.T2.
/2 width=3 by lifts_inv_flat1_aux/ qed-.

fact lifts_inv_sort2_aux: âˆ€X,Y,f. â¬†*[f] X â‰¡ Y â†’ âˆ€s. Y = â‹†s â†’ X = â‹†s.
#X #Y #f * -X -Y -f //
[ #i1 #i2 #f #_ #x #H destruct
| #p #I #V1 #V2 #T1 #T2 #f #_ #_ #x #H destruct
| #I #V1 #V2 #T1 #T2 #f #_ #_ #x #H destruct
]
qed-.

(* Basic_1: includes: lift_gen_sort *)
(* Basic_2A1: includes: lift_inv_sort2 *)
lemma lifts_inv_sort2: âˆ€X,s,f. â¬†*[f] X â‰¡ â‹†s â†’ X = â‹†s.
/2 width=4 by lifts_inv_sort2_aux/ qed-.

fact lifts_inv_lref2_aux: âˆ€X,Y,f. â¬†*[f] X â‰¡ Y â†’ âˆ€i2. Y = #i2 â†’
                          âˆƒâˆƒi1. @â¦ƒi1, fâ¦„ â‰¡ i2 & X = #i1.
#X #Y #f * -X -Y -f
[ #s #f #x #H destruct
| #i1 #i2 #f #Hi12 #x #H destruct /2 width=3 by ex2_intro/
| #l #f #x #H destruct
| #p #I #V1 #V2 #T1 #T2 #f #_ #_ #x #H destruct
| #I #V1 #V2 #T1 #T2 #f #_ #_ #x #H destruct
]
qed-.

(* Basic_1: includes: lift_gen_lref lift_gen_lref_lt lift_gen_lref_false lift_gen_lref_ge *)
(* Basic_2A1: includes: lift_inv_lref2 lift_inv_lref2_lt lift_inv_lref2_be lift_inv_lref2_ge lift_inv_lref2_plus *)
lemma lifts_inv_lref2: âˆ€X,i2,f. â¬†*[f] X â‰¡ #i2 â†’
                       âˆƒâˆƒi1. @â¦ƒi1, fâ¦„ â‰¡ i2 & X = #i1.
/2 width=3 by lifts_inv_lref2_aux/ qed-.

fact lifts_inv_gref2_aux: âˆ€X,Y,f. â¬†*[f] X â‰¡ Y â†’ âˆ€l. Y = Â§l â†’ X = Â§l.
#X #Y #f * -X -Y -f //
[ #i1 #i2 #f #_ #x #H destruct
| #p #I #V1 #V2 #T1 #T2 #f #_ #_ #x #H destruct
| #I #V1 #V2 #T1 #T2 #f #_ #_ #x #H destruct
]
qed-.

(* Basic_2A1: includes: lift_inv_gref1 *)
lemma lifts_inv_gref2: âˆ€X,l,f. â¬†*[f] X â‰¡ Â§l â†’ X = Â§l.
/2 width=4 by lifts_inv_gref2_aux/ qed-.

fact lifts_inv_bind2_aux: âˆ€X,Y,f. â¬†*[f] X â‰¡ Y â†’
                          âˆ€p,I,V2,T2. Y = â“‘{p,I}V2.T2 â†’
                          âˆƒâˆƒV1,T1. â¬†*[f] V1 â‰¡ V2 & â¬†*[â†‘f] T1 â‰¡ T2 &
                                   X = â“‘{p,I}V1.T1.
#X #Y #f * -X -Y -f
[ #s #f #q #J #W2 #U2 #H destruct
| #i1 #i2 #f #_ #q #J #W2 #U2 #H destruct
| #l #f #q #J #W2 #U2 #H destruct
| #p #I #V1 #V2 #T1 #T2 #f #HV12 #HT12 #q #J #W2 #U2 #H destruct /2 width=5 by ex3_2_intro/
| #I #V1 #V2 #T1 #T2 #f #_ #_ #q #J #W2 #U2 #H destruct
]
qed-.

(* Basic_1: includes: lift_gen_bind *)
(* Basic_2A1: includes: lift_inv_bind2 *)
lemma lifts_inv_bind2: âˆ€p,I,V2,T2,X,f. â¬†*[f] X â‰¡ â“‘{p,I}V2.T2 â†’
                       âˆƒâˆƒV1,T1. â¬†*[f] V1 â‰¡ V2 & â¬†*[â†‘f] T1 â‰¡ T2 &
                                X = â“‘{p,I}V1.T1.
/2 width=3 by lifts_inv_bind2_aux/ qed-.

fact lifts_inv_flat2_aux: âˆ€X,Y. âˆ€f:rtmap. â¬†*[f] X â‰¡ Y â†’
                          âˆ€I,V2,T2. Y = â“•{I}V2.T2 â†’
                          âˆƒâˆƒV1,T1. â¬†*[f] V1 â‰¡ V2 & â¬†*[f] T1 â‰¡ T2 &
                                   X = â“•{I}V1.T1.
#X #Y #f * -X -Y -f
[ #s #f #J #W2 #U2 #H destruct
| #i1 #i2 #f #_ #J #W2 #U2 #H destruct
| #l #f #J #W2 #U2 #H destruct
| #p #I #V1 #V2 #T1 #T2 #f #_ #_ #J #W2 #U2 #H destruct
| #I #V1 #V2 #T1 #T2 #f #HV12 #HT12 #J #W2 #U2 #H destruct /2 width=5 by ex3_2_intro/
]
qed-.

(* Basic_1: includes: lift_gen_flat *)
(* Basic_2A1: includes: lift_inv_flat2 *)
lemma lifts_inv_flat2: âˆ€I,V2,T2,X. âˆ€f:rtmap. â¬†*[f] X â‰¡ â“•{I}V2.T2 â†’
                       âˆƒâˆƒV1,T1. â¬†*[f] V1 â‰¡ V2 & â¬†*[f] T1 â‰¡ T2 &
                                X = â“•{I}V1.T1.
/2 width=3 by lifts_inv_flat2_aux/ qed-.

(* Basic_2A1: includes: lift_inv_pair_xy_x *)
lemma lifts_inv_pair_xy_x: âˆ€I,V,T,f. â¬†*[f] â‘¡{I}V.T â‰¡ V â†’ âŠ¥.
#J #V elim V -V
[ * #i #U #f #H
  [ lapply (lifts_inv_sort2 â€¦ H) -H #H destruct
  | elim (lifts_inv_lref2 â€¦ H) -H
    #x #_ #H destruct
  | lapply (lifts_inv_gref2 â€¦ H) -H #H destruct
  ]
| * [ #p ] #I #V2 #T2 #IHV2 #_ #U #f #H
  [ elim (lifts_inv_bind2 â€¦ H) -H #V1 #T1 #HV12 #_ #H destruct /2 width=3 by/
  | elim (lifts_inv_flat2 â€¦ H) -H #V1 #T1 #HV12 #_ #H destruct /2 width=3 by/
  ]
]
qed-.

(* Basic_1: includes: thead_x_lift_y_y *)
(* Basic_2A1: includes: lift_inv_pair_xy_y *)
lemma lifts_inv_pair_xy_y: âˆ€I,T,V,f. â¬†*[f] â‘¡{I}V.T â‰¡ T â†’ âŠ¥.
#J #T elim T -T
[ * #i #W #f #H
  [ lapply (lifts_inv_sort2 â€¦ H) -H #H destruct
  | elim (lifts_inv_lref2 â€¦ H) -H
    #x #_ #H destruct
  | lapply (lifts_inv_gref2 â€¦ H) -H #H destruct
  ]
| * [ #p ] #I #V2 #T2 #_ #IHT2 #W #f #H
  [ elim (lifts_inv_bind2 â€¦ H) -H #V1 #T1 #_ #HT12 #H destruct /2 width=4 by/
  | elim (lifts_inv_flat2 â€¦ H) -H #V1 #T1 #_ #HT12 #H destruct /2 width=4 by/
  ]
]
qed-.

(* Basic forward lemmas *****************************************************)

(* Basic_2A1: includes: lift_inv_O2 *)
lemma lifts_fwd_isid: âˆ€T1,T2,f. â¬†*[f] T1 â‰¡ T2 â†’ ğˆâ¦ƒfâ¦„ â†’ T1 = T2.
#T1 #T2 #f #H elim H -T1 -T2 -f
/4 width=3 by isid_inv_at_mono, isid_push, eq_f2, eq_f/
qed-.

(* Basic_2A1: includes: lift_fwd_pair1 *)
lemma lifts_fwd_pair1: âˆ€I,V1,T1,Y. âˆ€f:rtmap. â¬†*[f] â‘¡{I}V1.T1 â‰¡ Y â†’
                       âˆƒâˆƒV2,T2. â¬†*[f] V1 â‰¡ V2 & Y = â‘¡{I}V2.T2.
* [ #p ] #I #V1 #T1 #Y #f #H
[ elim (lifts_inv_bind1 â€¦ H) -H /2 width=4 by ex2_2_intro/
| elim (lifts_inv_flat1 â€¦ H) -H /2 width=4 by ex2_2_intro/
]
qed-.

(* Basic_2A1: includes: lift_fwd_pair2 *)
lemma lifts_fwd_pair2: âˆ€I,V2,T2,X. âˆ€f:rtmap. â¬†*[f] X â‰¡ â‘¡{I}V2.T2 â†’
                       âˆƒâˆƒV1,T1. â¬†*[f] V1 â‰¡ V2 & X = â‘¡{I}V1.T1.
* [ #p ] #I #V2 #T2 #X #f #H
[ elim (lifts_inv_bind2 â€¦ H) -H /2 width=4 by ex2_2_intro/
| elim (lifts_inv_flat2 â€¦ H) -H /2 width=4 by ex2_2_intro/
]
qed-.

(* Basic properties *********************************************************)

lemma lifts_eq_repl_back: âˆ€T1,T2. eq_repl_back â€¦ (Î»f. â¬†*[f] T1 â‰¡ T2).
#T1 #T2 #f1 #H elim H -T1 -T2 -f1
/4 width=5 by lifts_flat, lifts_bind, lifts_lref, at_eq_repl_back, eq_push/
qed-.

lemma lifts_eq_repl_fwd: âˆ€T1,T2. eq_repl_fwd â€¦ (Î»f. â¬†*[f] T1 â‰¡ T2).
#T1 #T2 @eq_repl_sym /2 width=3 by lifts_eq_repl_back/ (**) (* full auto fails *)
qed-.

(* Basic_1: includes: lift_r *)
(* Basic_2A1: includes: lift_refl *)
lemma lifts_refl: âˆ€T,f. ğˆâ¦ƒfâ¦„ â†’ â¬†*[f] T â‰¡ T.
#T elim T -T *
/4 width=3 by lifts_flat, lifts_bind, lifts_lref, isid_inv_at, isid_push/
qed.

(* Basic_2A1: includes: lift_total *)
lemma lifts_total: âˆ€T1,f. âˆƒT2. â¬†*[f] T1 â‰¡ T2.
#T1 elim T1 -T1 *
/3 width=2 by lifts_lref, lifts_sort, lifts_gref, ex_intro/
[ #p ] #I #V1 #T1 #IHV1 #IHT1 #f
elim (IHV1 f) -IHV1 #V2 #HV12
[ elim (IHT1 (â†‘f)) -IHT1 /3 width=2 by lifts_bind, ex_intro/
| elim (IHT1 f) -IHT1 /3 width=2 by lifts_flat, ex_intro/
]
qed-.

lemma lift_lref_uni: âˆ€l,i. â¬†*[l] #i â‰¡ #(l+i).
#l elim l -l /2 width=1 by lifts_lref/
qed.

(* Basic_1: includes: lift_free (right to left) *)
(* Basic_2A1: includes: lift_split *)
lemma lifts_split_trans: âˆ€T1,T2,f. â¬†*[f] T1 â‰¡ T2 â†’
                         âˆ€f1,f2. f2 âŠš f1 â‰¡ f â†’
                         âˆƒâˆƒT. â¬†*[f1] T1 â‰¡ T & â¬†*[f2] T â‰¡ T2.
#T1 #T2 #f #H elim H -T1 -T2 -f
[ /3 width=3 by lifts_sort, ex2_intro/
| #i1 #i2 #f #Hi #f1 #f2 #Ht elim (after_at_fwd â€¦ Hi â€¦ Ht) -Hi -Ht
  /3 width=3 by lifts_lref, ex2_intro/
| /3 width=3 by lifts_gref, ex2_intro/
| #p #I #V1 #V2 #T1 #T2 #f #_ #_ #IHV #IHT #f1 #f2 #Ht
  elim (IHV â€¦ Ht) elim (IHT (â†‘f1) (â†‘f2)) -IHV -IHT
  /3 width=5 by lifts_bind, after_O2, ex2_intro/
| #I #V1 #V2 #T1 #T2 #f #_ #_ #IHV #IHT #f1 #f2 #Ht
  elim (IHV â€¦ Ht) elim (IHT â€¦ Ht) -IHV -IHT -Ht
  /3 width=5 by lifts_flat, ex2_intro/
]
qed-.

(* Note: apparently, this was missing in Basic_2A1 *)
lemma lifts_split_div: âˆ€T1,T2,f1. â¬†*[f1] T1 â‰¡ T2 â†’
                       âˆ€f2,f. f2 âŠš f1 â‰¡ f â†’
                       âˆƒâˆƒT. â¬†*[f2] T2 â‰¡ T & â¬†*[f] T1 â‰¡ T.
#T1 #T2 #f1 #H elim H -T1 -T2 -f1
[ /3 width=3 by lifts_sort, ex2_intro/
| #i1 #i2 #f1 #Hi #f2 #f #Ht elim (after_at1_fwd â€¦ Hi â€¦ Ht) -Hi -Ht
  /3 width=3 by lifts_lref, ex2_intro/
| /3 width=3 by lifts_gref, ex2_intro/
| #p #I #V1 #V2 #T1 #T2 #f1 #_ #_ #IHV #IHT #f2 #f #Ht
  elim (IHV â€¦ Ht) elim (IHT (â†‘f2) (â†‘f)) -IHV -IHT
  /3 width=5 by lifts_bind, after_O2, ex2_intro/
| #I #V1 #V2 #T1 #T2 #f1 #_ #_ #IHV #IHT #f2 #f #Ht
  elim (IHV â€¦ Ht) elim (IHT â€¦ Ht) -IHV -IHT -Ht
  /3 width=5 by lifts_flat, ex2_intro/
]
qed-.

(* Basic_1: includes: dnf_dec2 dnf_dec *)
(* Basic_2A1: includes: is_lift_dec *)
lemma is_lifts_dec: âˆ€T2,f. Decidable (âˆƒT1. â¬†*[f] T1 â‰¡ T2).
#T1 elim T1 -T1
[ * [1,3: /3 width=2 by lifts_sort, lifts_gref, ex_intro, or_introl/ ]
  #i2 #f elim (is_at_dec f i2) //
  [ * /4 width=3 by lifts_lref, ex_intro, or_introl/
  | #H @or_intror *
    #X #HX elim (lifts_inv_lref2 â€¦ HX) -HX
    /3 width=2 by ex_intro/
  ]
| * [ #p ] #I #V2 #T2 #IHV2 #IHT2 #f
  [ elim (IHV2 f) -IHV2
    [ * #V1 #HV12 elim (IHT2 (â†‘f)) -IHT2
      [ * #T1 #HT12 @or_introl /3 width=2 by lifts_bind, ex_intro/
      | -V1 #HT2 @or_intror * #X #H
        elim (lifts_inv_bind2 â€¦ H) -H /3 width=2 by ex_intro/
      ]
    | -IHT2 #HV2 @or_intror * #X #H
      elim (lifts_inv_bind2 â€¦ H) -H /3 width=2 by ex_intro/
    ]
  | elim (IHV2 f) -IHV2
    [ * #V1 #HV12 elim (IHT2 f) -IHT2
      [ * #T1 #HT12 /4 width=2 by lifts_flat, ex_intro, or_introl/
      | -V1 #HT2 @or_intror * #X #H
        elim (lifts_inv_flat2 â€¦ H) -H /3 width=2 by ex_intro/
      ]
    | -IHT2 #HV2 @or_intror * #X #H
      elim (lifts_inv_flat2 â€¦ H) -H /3 width=2 by ex_intro/
    ]
  ]
]
qed-.

(* Basic_2A1: removed theorems 14:
              lifts_inv_nil lifts_inv_cons
              lift_inv_Y1 lift_inv_Y2 lift_inv_lref_Y1 lift_inv_lref_Y2 lift_lref_Y lift_Y1
              lift_lref_lt_eq lift_lref_ge_eq lift_lref_plus lift_lref_pred
              lift_lref_ge_minus lift_lref_ge_minus_eq
*)
(* Basic_1: removed theorems 8:
            lift_lref_gt            
            lift_head lift_gen_head 
            lift_weight_map lift_weight lift_weight_add lift_weight_add_O
            lift_tlt_dx
*)
