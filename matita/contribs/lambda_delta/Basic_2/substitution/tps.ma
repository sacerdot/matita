(**************************************************************************)
(*       ___                                                              *)
(*      ||M||                                                             *)
(*      ||A||       A project by Andrea Asperti                           *)
(*      ||T||                                                             *)
(*      ||I||       Developers:                                           *)
(*      ||T||         The HELM team.                                      *)
(*      ||A||         http://helm.cs.unibo.it                             *)
(*      \   /                                                             *)
(*       \ /        This file is distributed under the terms of the       *)
(*        v         GNU General Public License Version 2                  *)
(*                                                                        *)
(**************************************************************************)

include "Basic_2/grammar/cl_weight.ma".
include "Basic_2/substitution/ldrop.ma".

(* PARALLEL SUBSTITUTION ON TERMS *******************************************)

inductive tps: nat â†’ nat â†’ lenv â†’ relation term â‰
| tps_atom : âˆ€L,I,d,e. tps d e L (ğ•’{I}) (ğ•’{I})
| tps_subst: âˆ€L,K,V,W,i,d,e. d â‰¤ i â†’ i < d + e â†’
             â†“[0, i] L â‰¡ K. ğ•“{Abbr} V â†’ â†‘[0, i + 1] V â‰¡ W â†’ tps d e L (#i) W
| tps_bind : âˆ€L,I,V1,V2,T1,T2,d,e.
             tps d e L V1 V2 â†’ tps (d + 1) e (L. ğ•“{I} V2) T1 T2 â†’
             tps d e L (ğ•“{I} V1. T1) (ğ•“{I} V2. T2)
| tps_flat : âˆ€L,I,V1,V2,T1,T2,d,e.
             tps d e L V1 V2 â†’ tps d e L T1 T2 â†’
             tps d e L (ğ•—{I} V1. T1) (ğ•—{I} V2. T2)
.

interpretation "parallel substritution (term)"
   'PSubst L T1 d e T2 = (tps d e L T1 T2).

(* Basic properties *********************************************************)

lemma tps_lsubs_conf: âˆ€L1,T1,T2,d,e. L1 âŠ¢ T1 [d, e] â‰« T2 â†’
                      âˆ€L2. L1 [d, e] â‰¼ L2 â†’ L2 âŠ¢ T1 [d, e] â‰« T2.
#L1 #T1 #T2 #d #e #H elim H -H L1 T1 T2 d e
[ //
| #L1 #K1 #V #W #i #d #e #Hdi #Hide #HLK1 #HVW #L2 #HL12
  elim (ldrop_lsubs_ldrop1_abbr â€¦ HL12 â€¦ HLK1 ? ?) -HL12 HLK1 // /2/
| /4/
| /3/
]
qed.

lemma tps_refl: âˆ€T,L,d,e. L âŠ¢ T [d, e] â‰« T.
#T elim T -T //
#I elim I -I /2/
qed.

lemma tps_weak: âˆ€L,T1,T2,d1,e1. L âŠ¢ T1 [d1, e1] â‰« T2 â†’
                âˆ€d2,e2. d2 â‰¤ d1 â†’ d1 + e1 â‰¤ d2 + e2 â†’
                L âŠ¢ T1 [d2, e2] â‰« T2.
#L #T1 #T2 #d1 #e1 #H elim H -H L T1 T2 d1 e1
[ //
| #L #K #V #W #i #d1 #e1 #Hid1 #Hide1 #HLK #HVW #d2 #e2 #Hd12 #Hde12
  lapply (transitive_le â€¦ Hd12 â€¦ Hid1) -Hd12 Hid1 #Hid2
  lapply (lt_to_le_to_lt â€¦ Hide1 â€¦ Hde12) -Hide1 /2/
| /4/
| /4/
]
qed.

lemma tps_weak_top: âˆ€L,T1,T2,d,e.
                    L âŠ¢ T1 [d, e] â‰« T2 â†’ L âŠ¢ T1 [d, |L| - d] â‰« T2.
#L #T1 #T2 #d #e #H elim H -H L T1 T2 d e
[ //
| #L #K #V #W #i #d #e #Hdi #_ #HLK #HVW
  lapply (ldrop_fwd_ldrop2_length â€¦ HLK) #Hi
  lapply (le_to_lt_to_lt â€¦ Hdi Hi) #Hd
  lapply (plus_minus_m_m_comm (|L|) d ?) /2/
| normalize /2/
| /2/
]
qed.

lemma tps_weak_all: âˆ€L,T1,T2,d,e.
                    L âŠ¢ T1 [d, e] â‰« T2 â†’ L âŠ¢ T1 [0, |L|] â‰« T2.
#L #T1 #T2 #d #e #HT12
lapply (tps_weak â€¦ HT12 0 (d + e) ? ?) -HT12 // #HT12
lapply (tps_weak_top â€¦ HT12) //
qed.

lemma tps_split_up: âˆ€L,T1,T2,d,e. L âŠ¢ T1 [d, e] â‰« T2 â†’ âˆ€i. d â‰¤ i â†’ i â‰¤ d + e â†’
                    âˆƒâˆƒT. L âŠ¢ T1 [d, i - d] â‰« T & L âŠ¢ T [i, d + e - i] â‰« T2.
#L #T1 #T2 #d #e #H elim H -H L T1 T2 d e
[ /2/
| #L #K #V #W #i #d #e #Hdi #Hide #HLK #HVW #j #Hdj #Hjde
  elim (lt_or_ge i j)
  [ -Hide Hjde;
    >(plus_minus_m_m_comm j d) in âŠ¢ (% â†’ ?) // -Hdj /3/
  | -Hdi Hdj; #Hid
    generalize in match Hide -Hide (**) (* rewriting in the premises, rewrites in the goal too *)
    >(plus_minus_m_m_comm â€¦ Hjde) in âŠ¢ (% â†’ ?) -Hjde /4/
  ]
| #L #I #V1 #V2 #T1 #T2 #d #e #_ #_ #IHV12 #IHT12 #i #Hdi #Hide
  elim (IHV12 i ? ?) -IHV12 // #V #HV1 #HV2
  elim (IHT12 (i + 1) ? ?) -IHT12 [2: /2 by arith4/ |3: /2/ ] (* just /2/ is too slow *)
  -Hdi Hide >arith_c1 >arith_c1x #T #HT1 #HT2
  lapply (tps_lsubs_conf â€¦ HT1 (L. ğ•“{I} V) ?) -HT1 /3 width=5/
| #L #I #V1 #V2 #T1 #T2 #d #e #_ #_ #IHV12 #IHT12 #i #Hdi #Hide
  elim (IHV12 i ? ?) -IHV12 // elim (IHT12 i ? ?) -IHT12 //
  -Hdi Hide /3 width=5/
]
qed.

(* Basic inversion lemmas ***************************************************)

fact tps_inv_atom1_aux: âˆ€L,T1,T2,d,e. L âŠ¢ T1 [d, e] â‰« T2 â†’ âˆ€I. T1 = ğ•’{I} â†’
                        T2 = ğ•’{I} âˆ¨
                        âˆƒâˆƒK,V,i. d â‰¤ i & i < d + e &
                                 â†“[O, i] L â‰¡ K. ğ•“{Abbr} V &
                                 â†‘[O, i + 1] V â‰¡ T2 &
                                 I = LRef i.
#L #T1 #T2 #d #e * -L T1 T2 d e
[ #L #I #d #e #J #H destruct -I /2/
| #L #K #V #T2 #i #d #e #Hdi #Hide #HLK #HVT2 #I #H destruct -I /3 width=8/
| #L #I #V1 #V2 #T1 #T2 #d #e #_ #_ #J #H destruct
| #L #I #V1 #V2 #T1 #T2 #d #e #_ #_ #J #H destruct
]
qed.

lemma tps_inv_atom1: âˆ€L,T2,I,d,e. L âŠ¢ ğ•’{I} [d, e] â‰« T2 â†’
                     T2 = ğ•’{I} âˆ¨
                     âˆƒâˆƒK,V,i. d â‰¤ i & i < d + e &
                              â†“[O, i] L â‰¡ K. ğ•“{Abbr} V &
                              â†‘[O, i + 1] V â‰¡ T2 &
                              I = LRef i.
/2/ qed.


(* Basic_1: was: subst1_gen_sort *)
lemma tps_inv_sort1: âˆ€L,T2,k,d,e. L âŠ¢ â‹†k [d, e] â‰« T2 â†’ T2 = â‹†k.
#L #T2 #k #d #e #H
elim (tps_inv_atom1 â€¦ H) -H //
* #K #V #i #_ #_ #_ #_ #H destruct
qed.

(* Basic_1: was: subst1_gen_lref *)
lemma tps_inv_lref1: âˆ€L,T2,i,d,e. L âŠ¢ #i [d, e] â‰« T2 â†’
                     T2 = #i âˆ¨
                     âˆƒâˆƒK,V. d â‰¤ i & i < d + e &
                            â†“[O, i] L â‰¡ K. ğ•“{Abbr} V &
                            â†‘[O, i + 1] V â‰¡ T2.
#L #T2 #i #d #e #H
elim (tps_inv_atom1 â€¦ H) -H /2/
* #K #V #j #Hdj #Hjde #HLK #HVT2 #H destruct -i /3/
qed.

fact tps_inv_bind1_aux: âˆ€d,e,L,U1,U2. L âŠ¢ U1 [d, e] â‰« U2 â†’
                        âˆ€I,V1,T1. U1 = ğ•“{I} V1. T1 â†’
                        âˆƒâˆƒV2,T2. L âŠ¢ V1 [d, e] â‰« V2 & 
                                 L. ğ•“{I} V2 âŠ¢ T1 [d + 1, e] â‰« T2 &
                                 U2 =  ğ•“{I} V2. T2.
#d #e #L #U1 #U2 * -d e L U1 U2
[ #L #k #d #e #I #V1 #T1 #H destruct
| #L #K #V #W #i #d #e #_ #_ #_ #_ #I #V1 #T1 #H destruct
| #L #J #V1 #V2 #T1 #T2 #d #e #HV12 #HT12 #I #V #T #H destruct /2 width=5/
| #L #J #V1 #V2 #T1 #T2 #d #e #_ #_ #I #V #T #H destruct
]
qed.

lemma tps_inv_bind1: âˆ€d,e,L,I,V1,T1,U2. L âŠ¢ ğ•“{I} V1. T1 [d, e] â‰« U2 â†’
                     âˆƒâˆƒV2,T2. L âŠ¢ V1 [d, e] â‰« V2 & 
                              L. ğ•“{I} V2 âŠ¢ T1 [d + 1, e] â‰« T2 &
                              U2 =  ğ•“{I} V2. T2.
/2/ qed.

fact tps_inv_flat1_aux: âˆ€d,e,L,U1,U2. L âŠ¢ U1 [d, e] â‰« U2 â†’
                        âˆ€I,V1,T1. U1 = ğ•—{I} V1. T1 â†’
                        âˆƒâˆƒV2,T2. L âŠ¢ V1 [d, e] â‰« V2 & L âŠ¢ T1 [d, e] â‰« T2 &
                                 U2 =  ğ•—{I} V2. T2.
#d #e #L #U1 #U2 * -d e L U1 U2
[ #L #k #d #e #I #V1 #T1 #H destruct
| #L #K #V #W #i #d #e #_ #_ #_ #_ #I #V1 #T1 #H destruct
| #L #J #V1 #V2 #T1 #T2 #d #e #_ #_ #I #V #T #H destruct
| #L #J #V1 #V2 #T1 #T2 #d #e #HV12 #HT12 #I #V #T #H destruct /2 width=5/
]
qed.

lemma tps_inv_flat1: âˆ€d,e,L,I,V1,T1,U2. L âŠ¢ ğ•—{I} V1. T1 [d, e] â‰« U2 â†’
                     âˆƒâˆƒV2,T2. L âŠ¢ V1 [d, e] â‰« V2 & L âŠ¢ T1 [d, e] â‰« T2 &
                              U2 =  ğ•—{I} V2. T2.
/2/ qed.

fact tps_inv_refl_O2_aux: âˆ€L,T1,T2,d,e. L âŠ¢ T1 [d, e] â‰« T2 â†’ e = 0 â†’ T1 = T2.
#L #T1 #T2 #d #e #H elim H -H L T1 T2 d e
[ //
| #L #K #V #W #i #d #e #Hdi #Hide #_ #_ #H destruct -e;
  lapply (le_to_lt_to_lt â€¦ Hdi â€¦ Hide) -Hdi Hide <plus_n_O #Hdd
  elim (lt_refl_false â€¦ Hdd)
| /3/
| /3/
]
qed.

lemma tps_inv_refl_O2: âˆ€L,T1,T2,d. L âŠ¢ T1 [d, 0] â‰« T2 â†’ T1 = T2.
/2 width=6/ qed.

(* Basic forward lemmas *****************************************************)

lemma tps_fwd_tw: âˆ€L,T1,T2,d,e. L âŠ¢ T1 [d, e] â‰« T2 â†’ #[T1] â‰¤ #[T2].
#L #T1 #T2 #d #e #H elim H normalize -H L T1 T2 d e
[ //
| /2/
| /3 by monotonic_le_plus_l, le_plus/ (**) (* just /3/ is too slow *)
| /3 by monotonic_le_plus_l, le_plus/ (**) (* just /3/ is too slow *)
] 
qed.

(* Basic_1: removed theorems 25:
            subst0_gen_sort subst0_gen_lref subst0_gen_head subst0_gen_lift_lt
            subst0_gen_lift_false subst0_gen_lift_ge subst0_refl subst0_trans
            subst0_lift_lt subst0_lift_ge subst0_lift_ge_S subst0_lift_ge_s
            subst0_subst0 subst0_subst0_back subst0_weight_le subst0_weight_lt
            subst0_confluence_neq subst0_confluence_eq subst0_tlt_head
            subst0_confluence_lift subst0_tlt
            subst1_head subst1_gen_head subst1_lift_S subst1_confluence_lift 
*)
