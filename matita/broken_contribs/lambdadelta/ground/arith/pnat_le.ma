(**************************************************************************)
(*       ___                                                              *)
(*      ||M||                                                             *)
(*      ||A||       A project by Andrea Asperti                           *)
(*      ||T||                                                             *)
(*      ||I||       Developers:                                           *)
(*      ||T||         The HELM team.                                      *)
(*      ||A||         http://helm.cs.unibo.it                             *)
(*      \   /                                                             *)
(*       \ /        This file is distributed under the terms of the       *)
(*        v         GNU General Public License Version 2                  *)
(*                                                                        *)
(**************************************************************************)

include "ground/generated/insert_eq_1.ma".
include "ground/arith/pnat.ma".

(* ORDER FOR POSITIVE INTEGERS **********************************************)

inductive ple (p:â„¤âº): predicate (â„¤âº) â‰
| ple_refl   : ple p p
| ple_succ_dx: âˆ€q. ple p q â†’ ple p (â†‘q)
.

interpretation
  "less equal (positive integers)"
  'leq p q = (ple p q).

(* Basic constructions ******************************************************)

lemma ple_succ_dx_refl (p): p â‰¤ â†‘p.
/2 width=1 by ple_refl, ple_succ_dx/ qed.

lemma ple_unit_sx (p): ğŸ â‰¤ p.
#p elim p -p /2 width=1 by ple_succ_dx/
qed.

lemma ple_succ_bi (p) (q): p â‰¤ q â†’ â†‘p â‰¤ â†‘q.
#p #q #H elim H -q /2 width=1 by ple_refl, ple_succ_dx/
qed.

lemma pnat_split_le_ge (p) (q): âˆ¨âˆ¨ p â‰¤ q | q â‰¤ p.
#p #q @(pnat_ind_2 â€¦ p q) -p -q
[ /2 width=1 by or_introl/
| /2 width=1 by or_intror/
| #p #q * /3 width=2 by ple_succ_bi, or_introl, or_intror/
]
qed-.

(* Basic destructions *******************************************************)

lemma ple_des_succ_sn (p) (q): â†‘p â‰¤ q â†’ p â‰¤ q.
#p #q #H elim H -q /2 width=1 by ple_succ_dx/
qed-.

(* Basic inversions *********************************************************)

lemma ple_inv_succ_bi (p) (q): â†‘p â‰¤ â†‘q â†’ p â‰¤ q.
#p #q @(insert_eq_1 â€¦ (â†‘q))
#x * -x
[ #H destruct //
| #o #Ho #H destruct
  /2 width=1 by ple_des_succ_sn/ 
]
qed-.

lemma ple_inv_unit_dx (p): p â‰¤ ğŸ â†’ ğŸ = p.
#p @(insert_eq_1 â€¦ (ğŸ))
#y * -y
[ #H destruct //
| #y #_ #H destruct
]
qed-.

(* Advanced inversions ******************************************************)

lemma ple_inv_succ_unit (p): â†‘p â‰¤ ğŸ â†’ âŠ¥.
#p #H
lapply (ple_inv_unit_dx â€¦ H) -H #H destruct
qed-.

lemma ple_inv_succ_sn_refl (p): â†‘p â‰¤ p â†’ âŠ¥.
#p elim p -p [| #p #IH ] #H
[ /2 width=2 by ple_inv_succ_unit/
| /3 width=1 by ple_inv_succ_bi/
]
qed-.

theorem ple_antisym (p) (q): p â‰¤ q â†’ q â‰¤ p â†’ p = q.
#p #q #H elim H -q //
#q #_ #IH #Hq
lapply (ple_des_succ_sn â€¦ Hq) #H
lapply (IH H) -IH -H #H destruct
elim (ple_inv_succ_sn_refl â€¦ Hq)
qed-.

(* Advanced eliminations ****************************************************)

lemma ple_ind_alt (Q: relation2 â€¦):
      (âˆ€q. Q (ğŸ) (q)) â†’
      (âˆ€p,q. p â‰¤ q â†’ Q p q â†’ Q (â†‘p) (â†‘q)) â†’
      âˆ€p,q. p â‰¤ q â†’ Q p q.
#Q #IH1 #IH2 #p #q @(pnat_ind_2 â€¦ p q) -p -q //
[ #p #_ #H elim (ple_inv_succ_unit â€¦ H)
| /4 width=1 by ple_inv_succ_bi/
]
qed-.

(* Advanced constructions ***************************************************)

theorem ple_trans: Transitive â€¦ ple.
#p #q #H elim H -q /3 width=1 by ple_des_succ_sn/
qed-.

lemma ple_dec (p) (q): Decidable â€¦ (p â‰¤ q).
#p #q elim (pnat_split_le_ge p q) [ /2 width=1 by or_introl/ ]
#Hqp elim (eq_pnat_dec p q) [ #H destruct /2 width=1 by ple_refl, or_introl/ ]
/4 width=1 by ple_antisym, or_intror/
qed-.
