(**************************************************************************)
(*       ___                                                              *)
(*      ||M||                                                             *)
(*      ||A||       A project by Andrea Asperti                           *)
(*      ||T||                                                             *)
(*      ||I||       Developers:                                           *)
(*      ||T||         The HELM team.                                      *)
(*      ||A||         http://helm.cs.unibo.it                             *)
(*      \   /                                                             *)
(*       \ /        This file is distributed under the terms of the       *)
(*        v         GNU General Public License Version 2                  *)
(*                                                                        *)
(**************************************************************************)

include "ground/notation/functions/upspoon_1.ma".
include "ground/lib/stream.ma".
include "ground/arith/pnat.ma".

(* RELOCATION P-STREAM ******************************************************)

(* Basic inversion lemmas ***************************************************)

lemma eq_inv_gr_push_bi: injective ? ? gr_push.
#f1 #f2 <gr_push_unfold <gr_push_unfold #H destruct //
qed-.

lemma eq_inv_gr_push_next: ∀f1,f2. ⫯f1 = ↑f2 → ⊥.
#f1 * #p2 #f2 <gr_push_unfold <gr_next_unfold #H destruct
qed-.

lemma eq_inv_gr_next_push: ∀f1,f2. ↑f1 = ⫯f2 → ⊥.
* #p1 #f1 #f2 <gr_next_unfold <gr_push_unfold #H destruct
qed-.

lemma eq_inv_gr_next_bi: injective ? ? gr_next.
* #p1 #f1 * #p2 #f2 <gr_next_unfold <gr_next_unfold #H destruct //
qed-.

lemma case_prop (Q:predicate gr_map):
      (∀f. Q (⫯f)) → (∀f. Q (↑f)) → ∀f. Q f.
#Q #H1 #H2 * * //
qed-.

lemma case_type0 (Q:gr_map→Type[0]):
      (∀f. Q (⫯f)) → (∀f. Q (↑f)) → ∀f. Q f.
#Q #H1 #H2 * * //
qed-.

lemma iota_push: ∀Q,a,b,f. a f = case_type0 Q a b (⫯f).
// qed.

lemma iota_next: ∀Q,a,b,f. b f = case_type0 Q a b (↑f).
#Q #a #b * //
qed.
