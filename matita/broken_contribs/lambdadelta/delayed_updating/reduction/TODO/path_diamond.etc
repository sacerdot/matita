(**************************************************************************)
(*       ___                                                              *)
(*      ||M||                                                             *)
(*      ||A||       A project by Andrea Asperti                           *)
(*      ||T||                                                             *)
(*      ||I||       Developers:                                           *)
(*      ||T||         The HELM team.                                      *)
(*      ||A||         http://helm.cs.unibo.it                             *)
(*      \   /                                                             *)
(*       \ /        This file is distributed under the terms of the       *)
(*        v         GNU General Public License Version 2                  *)
(*                                                                        *)
(**************************************************************************)

include "delayed_updating/reduction/prototerm_reducibles.ma".
include "delayed_updating/reduction/dbf_step.ma".
include "delayed_updating/notation/relations/white_diamond_3.ma".

(* DISJOINT REDEXES *********************************************************)

definition path_drc: relation3 (𝕋) (ℙ) (ℙ) ≝
           λt0,r1,r2.
           ∧∧ r1 ϵ 𝐑❨t0❩ & r2 ϵ 𝐑❨t0❩ & (
              ∀t1,t2. t0 ➡𝐝𝐛𝐟[r1] t1 → t0 ➡𝐝𝐛𝐟[r2] t2 →
              ∃∃t. t1 ➡𝐝𝐛𝐟[r2] t & t2 ➡𝐝𝐛𝐟[r1] t
           ).

interpretation
  "disjoint redexes condition (path)"
  'WhiteDiamond r1 t0 r2 = (path_drc t0 r1 r2).

(* Basic constructions ******************************************************)

lemma path_drc_sym (t0):
      symmetric … (path_drc t0).
#t0 #r1 #r2 * #Hr1 #Hr2 #Hr
@and3_intro // -Hr1 -Hr2 #t2 #t1 #Ht2 #Ht1
elim (Hr … Ht1 Ht2) -Hr -Ht2 -Ht1 #t #Ht1 #Ht2
/2 width=3 by ex2_intro/
qed-.
